@AbapCatalog.sqlViewName: 'ZAA_EMP_VIEW'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Employee cds'
@Search.searchable : true

@UI:
{
 headerInfo:
  {
    typeName: 'Employee',
    typeNamePlural: 'Employees',
    title: { type: #STANDARD, value: 'eid' },
    description : { type: #STANDARD , value: 'ename' }
  }
 }

define root view ZAA_EMP_CDS
  as select from zaa_t_emp
  association [0..1] to ZAA_EMPTYPE_CDS as _EmpType  on $projection.etype = _EmpType.etype
  association [0..1] to zaa_gender_cds  as _Gender   on $projection.egender = _Gender.egender
  association [0..1] to I_Country       as _Country  on $projection.country = _Country.Country
  association [0..1] to I_Currency      as _Currency on $projection.currencycode = _Currency.Currency
{

      @UI.facet: [
      {
       id:       'Employee',
       purpose:  #STANDARD,
       type:     #IDENTIFICATION_REFERENCE,
       label:    'Employee',
       position: 10 }
      ]

      @UI: {
          lineItem: [ { position: 10, importance: #HIGH, label: 'Employee ID' } ]
          }
      @ObjectModel.readOnly: true
  key eid,
      @UI: {
        lineItem: [ { position: 20, label: 'First Name', importance: #HIGH } ],
        identification:[ { position: 10, label: 'First Name' } ]
      }
      @Search.defaultSearchElement: true
      efname,
      @UI: {
        lineItem: [ { position: 30, label: 'Last Name', importance: #HIGH } ],
        identification:[ { position: 10, label: 'Last Name' } ]
      }
      @Search.defaultSearchElement: true
      elname,
      @UI: {
        lineItem: [ { position: 40, label: 'Department', importance: #HIGH } ],
        identification:[ { position: 20, label: 'Department' } ],
        selectionField: [{ position: '10' }]
      }
      @EndUserText.label: 'Department'
      edept,
      @UI: {   identification:[ { position: 50, label: 'Gender' } ]  ,
         selectionField: [{ position: '20'}]  }
      @EndUserText.label: 'Gender'
      @Consumption.valueHelpDefinition: [{ entity: { name : 'ZAA_GENDER_CDS' , element: 'egender' }  }]
      egender,

      @UI: {   identification:[ { position: 60, label: 'Type' } ]  ,
            selectionField: [{ position: '30' }]  }
      @EndUserText.label: 'Employee Type'
      @Consumption.valueHelpDefinition: [{ entity: { name : 'ZAA_EMPTYPE_CDS' , element: 'etype' }  }]
      @ObjectModel.text.element: ['emptypeDesc']
      etype,
      @Semantics.text: true
      @UI.hidden: true
      _EmpType.etypedesc as emptypeDesc,

      @UI: {   identification:[ { position: 70, label: 'Email' , url: 'email' } ]
            }
      email,
      @Semantics.currencyCode: true
      @Consumption.valueHelpDefinition: [{ entity: { name : 'I_Currency' , element: 'currencycode' }  }]
      currencycode,
      @UI: {   identification:[ { position: 90, label: 'Salary' } ]    }
      @Semantics.amount.currencyCode: 'currencycode'
      esalary,
      @UI: {  lineItem: [{ type: #FOR_ACTION , dataAction: 'set_favorite' , label: 'Set Favorite' }] }
      favorite,
      @UI: {   identification:[ { position: 100, label: 'Country' } ]    }
      @Consumption.valueHelpDefinition: [{ entity: { name : 'I_Country' , element: 'country' }  }]
      country,
      @UI: {   identification:[ { position: 110, label: 'DOB' } ]    }      
      dob,
      lastchangeby,
      lastchangedat,

      _EmpType,
      _Gender,
      _Country,
      _Currency
}
